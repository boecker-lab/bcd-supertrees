import java.nio.file.Paths

plugins {
    id "com.jfrog.artifactory" version "4.3.0"
}

project.ext.set("distPath", file('dist').absolutePath)
project.ext.set("readmePath", file('README.md').absolutePath)
project.ext.set("copyingPath", file('COPYING.txt').absolutePath)
project.ext.set("licensePath", file('LICENSE.txt').absolutePath)
project.ext.set("jarName", 'BCDSupertrees.jar')
project.ext.set("baseName", 'bcdSupertrees')

allprojects {

    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: "com.jfrog.artifactory"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    group = 'de.unijena.bioinf.phylo'

    version = '1.1.0'

    repositories {
        mavenCentral()
        mavenLocal()
//        maven { url "http://bio.informatik.uni-jena.de/artifactory/repo" }
        maven { url "http://bio.informatik.uni-jena.de/repository/local" }
    }

    dependencies {
        compile 'de.unijena.bioinf.phylo:phyloTree-lib.model:0.9.9'
        compile 'de.unijena.bioinf.phylo:phyloTree-lib.algorithm:0.9.9'
        compile 'de.unijena.bioinf.phylo:phyloTree-lib.utils:0.9.9'


        compile 'net.openhft:affinity:3.0'
        compile group: 'org.roaringbitmap', name: 'RoaringBitmap', version: '0.6.51'
        compile group: 'org.apache.commons', name: 'commons-collections4', version: '4.1'

        testCompile 'de.unijena.bioinf.phylo:phyloTree-lib.utils:0.9.9'
        testCompile 'junit:junit:4.11'

    }
}

subprojects {
    task sourcesJar(type: Jar, dependsOn: classes) {
        group = 'build'
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        group = 'documentation'
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                if (version.toString().endsWith('-SNAPSHOT'))
                    setArtifacts([jar, sourcesJar])
                else
                    setArtifacts([jar, javadocJar, sourcesJar])

            }
        }
    }
}


//Core Project specific stuff
project(':flipcut-core') {
    dependencies {
        compile project(':mincut')
    }
}

//single algo Project specific stuff
project(':flipcut-lib') {
    dependencies {
        compile project(':flipcut-core')
        testCompile 'de.unijena.bioinf.phylo:gscm-lib:1.0.1'
    }
}


//beam algo Project specific stuff
project(':flipcut-lib.beam-search') {
    dependencies {
        compile project(':flipcut-lib')
        testCompile 'de.unijena.bioinf.phylo:gscm-lib:1.0.1'
    }
}

dependencies {
    compile project(':flipcut-core')
    compile project(':flipcut-lib')
    compile project(':flipcut-lib.beam-search')
    compile project(':flipcut-cli')
    compile project(':flipcut-cli.beam-search')
}



publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            setArtifacts([])
        }
    }
}

artifactory {
    contextUrl = 'https://bio.informatik.uni-jena.de/repository/'
    publish {
        repository {
            if (version.toString().endsWith('-SNAPSHOT'))
                repoKey = 'libs-snapshot-local'
            else
                repoKey = 'libs-release-local'
            Properties properties = new Properties()
            File propertiesFile = Paths.get(System.properties['user.home'], 'gradle.properties').toFile()
            propertiesFile.withInputStream {
                properties.load(it)
            }

            username = properties['de.unijena.bioinf.build.artifactory.username']
            password = properties['de.unijena.bioinf.build.artifactory.password']
        }
        defaults {
            publications('mavenJava')
        }
    }
}











